
rcvfile2: procedure options (main);

	dcl     MAIN		 char (8) static options (constant) init ("rcvfile2");

	dcl     true		 bit static options (constant) init ("1"b);
	dcl     false		 bit static options (constant) init ("0"b);

	dcl     STX		 char (1) static options (constant) init ("\002"); /* Start of text */
	dcl     ETX		 char (1) static options (constant) init ("\003"); /* End of text */
	dcl     EOT		 char (1) static options (constant) init ("\004"); /* End of transmission */
	dcl     ENQ		 char (1) static options (constant) init ("\005"); /* Enquiry */
	dcl     ACK		 char (1) static options (constant) init ("\006"); /* Acknowledge */
	dcl     ETB		 char (1) static options (constant) init ("\027"); /* End of transmit block */
	dcl     CAN		 char (1) static options (constant) init ("\030"); /* Cancel */
	dcl     EM		 char (1) static options (constant) init ("\031"); /* End of medium */

	dcl     cu_$arg_count	 entry (fixed bin, fixed bin (35));

	dcl     cu_$arg_ptr 	 entry (fixed bin, ptr, fixed bin (21), fixed bin (35));
	dcl     argn		 fixed bin init (0);
	dcl     argp		 ptr init (null ());
	dcl     argl		 fixed bin (21) init (0);
	dcl     arg		 char (argl) based (argp);

	dcl     XFER_MODES		 char (512) static options (constant)
				 init ("breakall,rawo,rawi,ctl_char,^echoplex,^lfecho,^crecho,^replay,^polite");
	dcl     current_modes	 char (512);

	dcl     buffer		 char (1024) varying;
	dcl     buffer_size 	 fixed bin;
	dcl     n_read		 fixed bin (21);
	dcl     code		 fixed bin (35);

	dcl     out_file_name	 char (32) varying init ("");
	dcl     out_file		 file;
	dcl     num_lines		 fixed bin init (0);
	dcl     max_chars		 fixed bin init (0);
	dcl     line_done		 bit;
	dcl     completed		 bit;

	dcl     timer_manager_$alarm_call entry (fixed bin (71), bit (2), entry);
	dcl     timer_manager_$reset_alarm_call entry (entry);
	dcl     timeout		 fixed bin (71) static options (constant) init (10);
	dcl     seconds		 bit (2) static options (constant) init ("11"b);

	dcl     com_err_		 entry options (variable);
	dcl     ioa_		 entry options (variable);

	dcl     error		 condition;

	call cu_$arg_count (argn, code);
	if ^success (code) then return;

	if argn = 1 then do;
		call cu_$arg_ptr (1, argp, argl, code);
		if ^success (code) then return;
		out_file_name = arg;
	     end;
	else do;
		call ioa_ ("Usage: ^a [filename]", MAIN);
		return;
	     end;

	call iox_$modes (iox_$user_io, XFER_MODES, current_modes, code);
	if ^success (code) then return;

	completed = false;

	on error goto xfer_done_cleanup;

	call wait_for (STX, timeout);
	call send_out (ACK);

	buffer = "";

	call wait_for (STX, timeout);
	call get_frame (buffer, timeout, line_done);
	call send_out (ACK);
	num_lines = decimal (buffer);

	buffer = "";

	call wait_for (STX, timeout);
	call get_frame (buffer, timeout, line_done);
	call send_out (ACK);
	max_chars = decimal (buffer);

	buffer_size = (size (buffer) - 1) * 4;
	if max_chars > buffer_size then do;
		call ioa_ ("Longest line exceeds ^d characters.", buffer_size);
		goto xfer_done_cleanup;
	     end;

	open file (out_file) linesize (max_chars) pagesize (num_lines)
	     title ("vfile_ " || out_file_name) stream output;

	buffer = "";
	do while (buffer ^= EM);
	     call wait_for (STX, timeout);
	     call get_frame (buffer, timeout, line_done);
	     if buffer ^= EM then do;
		     if line_done then do;
			     write file (out_file) from (buffer);
			     buffer = "";
			end;
		     call send_out (ACK);
		end;
	end;

	completed = true;
	goto xfer_done_cleanup;

timeout_handler: entry;
	call ioa_ ("Timeout expired.");
	goto xfer_done_cleanup;

wait_for: procedure (bytecode, timeout);
	dcl     bytecode		 char (1);
	dcl     timeout		 fixed bin (71);
	dcl     buffer		 char (1) init ("");

	call timer_manager_$alarm_call (timeout, seconds, timeout_handler);
	do while (buffer ^= bytecode);
	     call iox_$get_chars (iox_$user_input, addr (buffer), 1, n_read, code);
	     call timer_manager_$reset_alarm_call (timeout_handler);
	     if ^success (code) | (buffer = CAN) then goto xfer_done_cleanup;
	end;
     end wait_for;

send_out: procedure (bytecode);
	dcl     bytecode		 char (1);

	call iox_$put_chars (iox_$user_output, addr (bytecode), 1, code);
	if ^success (code) then goto xfer_done_cleanup;
     end send_out;

get_frame: procedure (return_buffer, timeout, line_done);
	dcl     return_buffer	 char (*) varying;
	dcl     timeout		 fixed bin (71);
	dcl     line_done		 bit (1);
	dcl     input_byte		 char (1) init ("");

	line_done = false;
	call timer_manager_$alarm_call (timeout, seconds, timeout_handler);
	do while (input_byte ^= EOT);
	     call iox_$get_chars (iox_$user_input, addr (input_byte), 1, n_read, code);
	     call timer_manager_$reset_alarm_call (timeout_handler);
	     if ^success (code) | (input_byte = CAN) then goto xfer_done_cleanup;
	     if input_byte ^= EOT then do;
		     if input_byte = ETB then line_done = true;
		     else return_buffer = return_buffer || input_byte;
		end;
	end;
     end get_frame;

success: procedure (code) returns (bit);
	dcl     code		 fixed bin (35);

	if code = 0 then return (true);
	call com_err_ (code, MAIN);
	return (false);
     end success;

xfer_done_cleanup:
	call iox_$modes (iox_$user_io, current_modes, "", code);
	close file (out_file);
	if ^completed then call ioa_ ("Transfer aborted.");

%include iox_dcls;

     end rcvfile2;
